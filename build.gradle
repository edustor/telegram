import org.ajoberstar.grgit.Grgit

import java.time.Instant

buildscript {
    ext.kotlin_version = '1.0.5-2'
    ext.spring_boot_version = '1.4.1.RELEASE'
    ext.edustor_commons_version = '45050d7'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

plugins {
    id "com.palantir.configuration-resolver" version "0.1.0"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'

group 'ru.edustor'
version '0.6.0'


repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

//noinspection GroovyAssignabilityCheck
jar {
    destinationDir = new File(buildDir, "/dist")
    archiveName = "edustor.jar"
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

ext {
    def grgit = Grgit.open()
    shortCommitId = grgit.head().getAbbreviatedId()
    commitId = grgit.head().id
}

task createProperties(dependsOn: processResources) {
    doLast {
        def buildPropFile = new File("$buildDir/resources/main/build.properties")
        buildPropFile.parentFile.mkdirs()
        buildPropFile.withWriter { w ->
            def p = new Properties()
            p['edustor.build.version'] = "${project.version}-$shortCommitId".toString()
            p['edustor.build.date'] = Instant.now().toString()
            p['edustor.build.commitId'] = commitId
            p.store(w, null)
        }
    }
}

processResources.finalizedBy createProperties

dependencies {
    compile "ru.edustor.commons:rabbit:$edustor_commons_version"
    compile "ru.edustor.commons:models:$edustor_commons_version"
    compile "ru.edustor.commons:api:$edustor_commons_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-starter-amqp"
    compile 'com.github.pengrad:java-telegram-bot-api:2.1.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-core:2.1.0-RC.1'
    testCompile 'com.nhaarman:mockito-kotlin:0.6.1'
}